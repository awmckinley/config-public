#!{{ lookPath "nu" }}
#
# Initialize machine.

def success [command?: closure]: [nothing -> bool, closure -> bool] {
	let command = default $command
	do $command | complete | $in.exit_code == 0
}

def prereq-go [] {
	if (which go) != [] {
		go env -w GOBIN=$"($env.HOME)/.local/bin"
	}
}

def init-aerc [] {
{{ if eq .chezmoi.os "darwin" }}
	use aerc.nu *
	let grouped = open $"($env.HOME)/.config/maildir.json" | group-by Profile
	aerc-aliases ($grouped | columns) | save --force $"($env.HOME)/.profile.d/aerc.zsh"
	for key in ($grouped | columns) {
		let accounts = $grouped | get $key
		aerc-config $accounts | save --force $"($env.HOME)/.config/aerc/accounts.($key).conf"
	}
{{ end }}
}

def init-aliae [] {
	if (which aliae) == [] {
		print "Install aliae"
		curl -s https://aliae.dev/install.sh | bash -s -- -d ~/.local/bin
		print ""
	}
}

def init-bat [] {
	if (which bat) != [] {
		bat cache --build
	}
}

def init-bootdev [] {
{{ if ne .chezmoi.os "windows" }}
	if (which bootdev) == [] {
		prereq-go
		print "Install bootdev"
		go install github.com/bootdotdev/bootdev@latest
		print ""
	}
{{ end }}
}

def init-burntcookie [] {
{{ if eq .chezmoi.os "darwin" }}
	if (which burntcookie) == [] {
		print "Install burntcookie"
		cargo install --git https://github.com/horrorho/burnt-cookie.git
		print ""
	}
{{ end }}
}

def init-committed [] {
	if (which committed) == [] {
		print "Install committed"
		cargo install committed
		print ""
	}
}

def init-darkman [] {
	if (which dconf) != [] {
		dconf write /org/gnome/desktop/interface/color-scheme "'prefer-dark'"
	}
}

def init-goimapnotify [] {
{{ if eq .chezmoi.os "darwin" }}
	use goimapnotify.nu *
	let accounts = open $"($env.HOME)/.config/maildir.json"
	goimapnotify-config $accounts | save --force $"($env.HOME)/.config/goimapnotify/goimapnotify.yaml"
{{ end }}
}

def init-mani [] {
	if (which mani) == [] {
		prereq-go
		print "Install mani"
		go install github.com/alajmo/mani@latest
		print ""
	}
}

def init-mbsync [] {
{{ if eq .chezmoi.os "darwin" }}
	use mbsync.nu *
	let accounts = open $"($env.HOME)/.config/maildir.json"
	mbsync-rc $accounts | save --force $"($env.HOME)/.mbsyncrc"
{{ end }}
}

def init-opam [] {
	if (which opam) == [] {
		return
	}

	if not ($"($env.HOME)/.opam" | path exists) {
		opam init --disable-sandboxing --no-setup
	}

	opam option --global depext=false

	if (which ocamlformat) == [] {
		opam install --yes ocamlformat
	}

	if (which ocamllsp) == [] {
		opam install --yes ocaml-lsp-server
	}
}

def init-pdm [] {
	if (which pdm) != [] {
		pdm config venv.backend venv
	}
}

def init-tailscale [] {
{{ if .decrypt }}
	if (which tailscale) == [] {
		return
	}

	if not (success { tailscale status --peers=false }) {
		tailscale up
	}
{{ end }}
}

def init-tldr [] {
	if (which tldr) != [] {
		tldr --update
	}
}

def init-whisper-cpp [model: string] {
	if (which whisper-cpp-download-ggml-model) == [] {
		return
	}

	let modelhome = $"($env.HOME)/.local/share/whisper-cpp"
	if not ($"($modelhome)/ggml-($model).bin" | path exists) {
		whisper-cpp-download-ggml-model $model
		mkdir $modelhome
		mv $"ggml-($model).bin" $modelhome
	}
}

def init-winget [] {
	if (which winget) != [] {
		winget update --all --include-unknown
	}
}

def init-zsh [] {
{{ if ne .chezmoi.os "windows" }}
	let zgenom = $"($env.HOME)/.zgenom"
	if not ($zgenom | path exists) {
		print "Setup zgenom"
		git clone --single-branch https://github.com/jandamm/zgenom.git --depth 1 $zgenom
		print ""
	}
{{ end }}
}

def main [] {
	init-aerc
	init-aliae
	init-bat
	init-bootdev
	init-burntcookie
	init-committed
	init-darkman
	init-goimapnotify
	init-mani
	init-mbsync
	init-opam
	init-pdm
	init-tailscale
	init-tldr
	init-whisper-cpp "small.en"
	init-zsh
}
